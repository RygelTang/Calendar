apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'io.objectbox'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    defaultConfig {
        applicationId rootProject.ext.application_id
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner rootProject.ext.testInstrumentationRunner
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            buildConfigField "boolean", "LOG_DEBUG", "false"
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation rootProject.ext.support.app_compat
    implementation rootProject.ext.support.v4
    implementation rootProject.ext.support.v7
    implementation rootProject.ext.support.design
    implementation rootProject.ext.support.media_compat
    implementation rootProject.ext.support.recyclerview
    implementation rootProject.ext.support.constraint_layout
    implementation rootProject.ext.support.vector_drawable
    implementation rootProject.ext.support.grid

    // 图片加载
    implementation rootProject.ext.glide.glide
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    annotationProcessor rootProject.ext.glide.compiler

    // 日志
    implementation rootProject.ext.logger

    // 网络请求
    implementation rootProject.ext.retrofit.retrofit
    implementation rootProject.ext.retrofit.adapter_rxjava
    implementation rootProject.ext.retrofit.adapter_gson

    implementation rootProject.ext.okhttp.okhttp
    implementation rootProject.ext.okhttp.log_interceptor

    //请求解析
    implementation rootProject.ext.gson

    // 换肤框架
    implementation rootProject.ext.scoops.scoops
    annotationProcessor rootProject.ext.scoops.compiler

    // ButterKnife
    implementation rootProject.ext.butter_knife.butter_knife
    annotationProcessor rootProject.ext.butter_knife.compiler

    // 内存泄漏
    debugImplementation rootProject.ext.leak_canary.leak_canary
    releaseImplementation rootProject.ext.leak_canary.no_op
    // Optional, if you use support library fragments:
    debugImplementation rootProject.ext.leak_canary.support_fragment

    // EventBus
    implementation rootProject.ext.event_bus

    // 屏幕适配
    implementation rootProject.ext.auto_size

    // multi dex
    implementation rootProject.ext.multidex

    // mmkv 用于替换 sp
    implementation rootProject.ext.mmkv

    // 选择器
    implementation rootProject.ext.picker

    // material switch
    implementation rootProject.ext.switch_button

    // 图片选择框架
    implementation rootProject.ext.matisse

    // UI基础库
    implementation project(':uilibrary')

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
