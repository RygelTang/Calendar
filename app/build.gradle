apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'bugly'
apply from: 'andres.gradle'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    defaultConfig {
        applicationId rootProject.ext.application_id
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner rootProject.ext.testInstrumentationRunner
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        ndk {
            abiFilters 'armeabi-v7a'
        }

    }
    buildTypes {
        release {
            multiDexKeepFile file('multidex-config.txt')
            signingConfig signingConfigs.release
            buildConfigField "boolean", "LOG_DEBUG", "false"
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

bugly {
    appId = keystoreProperties['bugly_id']
    appKey = keystoreProperties['bugly_key']
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // support
    implementation rootProject.ext.support.design
    implementation rootProject.ext.support.recyclerview
    implementation rootProject.ext.support.constraint_layout
    implementation rootProject.ext.support.palette

    // 图片加载
    implementation rootProject.ext.glide.glide
    annotationProcessor rootProject.ext.glide.compiler

    // 工具类
    implementation rootProject.ext.utils

    // 日志
    implementation rootProject.ext.logger

    // ButterKnife
    implementation rootProject.ext.butter_knife.butter_knife
    annotationProcessor rootProject.ext.butter_knife.compiler

    // 内存泄漏
    debugImplementation rootProject.ext.leak_canary.leak_canary
    releaseImplementation rootProject.ext.leak_canary.no_op
    // Optional, if you use support library fragments:
    debugImplementation rootProject.ext.leak_canary.support_fragment

    // EventBus
    implementation rootProject.ext.event_bus

    // 屏幕适配
    implementation rootProject.ext.auto_size

    // multi dex
    implementation rootProject.ext.multidex

    // mmkv 用于替换 sp
    implementation rootProject.ext.mmkv

    // 选择器
    // implementation rootProject.ext.picker

    // material switch
    implementation rootProject.ext.switch_button

    // 图片选择框架
    // implementation rootProject.ext.matisse
    implementation rootProject.ext.boxing.core
    implementation rootProject.ext.boxing.impl
    implementation rootProject.ext.ucrop

    // 日历
    implementation rootProject.ext.calendar
    implementation rootProject.ext.calendar_utils
    implementation rootProject.ext.date_picker

    // indicator
    implementation rootProject.ext.indicator

    // spinner
    implementation rootProject.ext.spinner

    // permission
    implementation rootProject.ext.permission

    // 换肤框架
    implementation rootProject.ext.theme.support
    implementation rootProject.ext.theme.design

    // bugly
    implementation rootProject.ext.bugly.update
    implementation rootProject.ext.bugly.bugly_native

    // object box
    debugImplementation rootProject.ext.object_box.browser
    releaseImplementation rootProject.ext.object_box.android

    // keep alive
    implementation rootProject.ext.keep_alive

    // UI基础库
    implementation project(':uilibrary')

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

// 官网要求apply必须在dependencies block之后
// apply the plugin after the dependencies block
apply plugin: 'io.objectbox'
